# SIMON算法轮函数汇编实现分析

## 对16位的循环左移操作

本部分讨论不同循环左移位数的最优实现方案，这里的“最优方案”仅考虑最少的汇编指令条数。 

以下操作均对0x6544进行循环移位，待移位数据保存在20H, 21H，移位结果保存在26H,27H。
```
    MOV 0x20, #0x65
    MOV 0x21, #0x44
```

### 循环左移8位

直接交换
```
    MOV 0x26, 0x21
    MOV 0x27, 0x20
```

### 循环左移1位、9位

1. 循环左移1位
```
    MOV A, 0x20
    MOV C, 21H.7
    RLC A
    MOV 0x26, A
    MOV A, 0x21
    RLC A
    MOV 0x27, A
```

2. 循环左移9位
```
    MOV A, 0x21
    MOV C, 20H.7
    RLC A
    MOV 0x26, A
    MOV A, 0x20
    RLC A
    MOV 0x27, A
```
循环左移1位和9位，依靠两次带进位左移实现。

### 循环左移15位、7位

1. 循环左移15位
```
    MOV A, 0x20 
    MOV C , 21H.0
    RRC A
    MOV 0x26, A
    MOV A, 21H
    RRC A
    MOV 0x27, A
```

2. 循环左移7位
```
    MOV A, 0x21 
    MOV C , 20H.0
    RRC A
    MOV 0x26, A
    MOV A, 0x20
    RRC A
    MOV 0x27, A
```
循环左移15位和7位，依靠两次带进位右移实现。

### 其他循环左移位数 

剩下的循环左移位数实现的思想依靠乘、除、与、或等操作，按照要左移的位数把16比特划分为4个区间（乘、除、与实现），把4个区间重新组合（或实现）。下面代码展示了如何循环左移两比特。
```
    MOV A, 0x20
    ANL A, #0xC0
    MOV B, #0x40
    DIV AB
    MOV 0x0A, A  ;tmp1

    MOV A, 0x20
    ANL A, #0x3F
    MOV B, #0x04
    MUL AB
    MOV 0x0B, A ;tmp2

    MOV A, 0x21
    ANL A, #0xC0
    MOV B, #0x40
    DIV AB
    MOV 0x0C, A ;tmp3

    MOV A, 0x21
    ANL A, #0x3F
    MOV B, #0x04
    MUL AB
    MOV 0x0D, A ;tmp4

    MOV A, 0x0B
    ORL A, 0x0C
    MOV 0x26, A
    MOV A, 0x0A
    ORL A, 0x0D
    MOV 0x27, A
```

## SIMON第一轮完整汇编实现 

```
    MOV 0x20, #0x65  ;L
    MOV 0x21, #0x44
    MOV 0x22, #0x68  ;R
    MOV 0x23, #0x77 
    MOV 0x24, #0x01  ;K
    MOV 0x25, #0x00

    ; 循环左移1位 
    MOV A, 0x20
    MOV C, 21H.7
    RLC A
    MOV 0x26, A
    MOV A, 0x21
    RLC A
    MOV 0x27, A

    ；循环左移8位加与操作
    MOV A, 0x26
    ANL A, 0x21
    MOV 0x26, A
    MOV A, 0x27
    ANL A, 0x20
    MOV 0x27, A

    ；与R异或
    MOV A, 0x26
    XRL A, 0x22
    MOV 0x26, A
    MOV A, 0x27
    XRL A, 0x23
    MOV 0x27, A

    ；循环左移两位加异或操作
    MOV A, 0x20
    ANL A, #0xC0
    MOV B, #0x40
    DIV AB
    MOV 0x0A, A  ;tmp1

    MOV A, 0x20
    ANL A, #0x3F
    MOV B, #0x04
    MUL AB
    MOV 0x0B, A ;tmp2

    MOV A, 0x21
    ANL A, #0xC0
    MOV B, #0x40
    DIV AB
    MOV 0x0C, A ;tmp3

    MOV A, 0x21
    ANL A, #0x3F
    MOV B, #0x04
    MUL AB
    MOV 0x0D, A ;tmp4

    MOV A, 0x0B
    ORL A, 0x0C
    XRL A, 0x26
    MOV 0x26, A
    MOV A, 0x0A
    ORL A, 0x0D
    XRL A, 0x27
    MOV 0x27, A

    ；与k异或
    MOV A, 0x24
    XRL A, 0x26
    MOV 0x26, A
    MOV A, 0x25
    XRL A, 0x27
    MOV 0x27, A

```


